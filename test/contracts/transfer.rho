// https://tgrospic.github.io/rnode-client-js-dev-test/
new deployId(`rho:rchain:deployId`), result in {
  contract @"Transfer"(deployer, @fromAddr, @toAddr, @amount, return) = {
    new rl(`rho:registry:lookup`), RevVaultCh in {
      rl!(`rho:rchain:revVault`, *RevVaultCh) |
      for (@(_, RevVault) <- RevVaultCh) {
        new vaultCh, vaultTo, revVaultkeyCh in {
          @RevVault!("findOrCreate", fromAddr, *vaultCh) |
          @RevVault!("findOrCreate", toAddr, *vaultTo) |
          @RevVault!("deployerAuthKey", *deployer, *revVaultkeyCh) |
          for (@vault <- vaultCh; key <- revVaultkeyCh; _ <- vaultTo) {
            match vault {
              (true, vault) => {
                new resultCh in {
                  @vault!("transfer", toAddr, amount, *key, *resultCh) |
                  for (@result <- resultCh) {
                    match result {
                      (true , _  ) => return!((true, "Transfer successful"))
                      (false, err) => return!((false, err))
                    }
                  }
                }
              }
              err => {
                return!((false, "REV vault cannot be found or created."))
              }
            }
          }
        }
      }
    }
  } |
  new deployerId(`rho:rchain:deployerId`) in {
     @"Transfer"!(*deployerId, "1111dutpn66C9c8idredGotkAQddh2N62hxBRSaSSBD9D76dwQpTm", "1111mpURtp4Vt88BFu6FrfcyQtE28EC3NTvo6SbYebWMg1MLkMgaj", 123456, *deployId)
  }
}